# File name: PCT-EP-OFF
# File content: Official fees for PCT regional phase: EPO
# Valid until 31/03/2023
# Currency: EUR

DEFINE NUMBER SheetCount AS 'Number of sheets?'
BETWEEN 1 AND 100000
DEFAULT 15
ENDDEFINE

DEFINE NUMBER ClaimCount AS 'Number of claims?'
BETWEEN 1 AND 100
DEFAULT 15
ENDDEFINE

# International Search Authority
DEFINE LIST ISA AS 'Which ISA searched the PCT application?'
CHOICE ISA_NONE AS 'NONE OF THE LIST'
CHOICE ISA_EPO AS 'EPO'
CHOICE ISA_AT AS 'AT PO'
CHOICE ISA_FI AS 'FI PRH'
CHOICE ISA_NPI AS 'NPI'
CHOICE ISA_ES AS 'ES OEPM'
CHOICE ISA_SE AS 'SE PRV' 
CHOICE ISA_TR AS 'TR Turkpatent'
CHOICE ISA_VI AS 'Visegrad Patent Institute'
DEFAULT ISA_NONE
ENDDEFINE

# International preliminary report on patentability (Chapter II of the PCT)
DEFINE LIST IPRP AS 'Which office provided an IPRP (Chapter II)?'
CHOICE IPRP_NONE AS 'NONE'
CHOICE IPRP_EPO AS 'EPO'
CHOICE IPRP_USPTO AS 'USPTO'
# supplementing other IPEAs needed
DEFAULT IPRP_NONE
ENDDEFINE

DEFINE BOOLEAN Extension AS 'Designate Bosnia and Herzegovina?'
DEFAULT FALSE
ENDDEFINE

DEFINE BOOLEAN VAL_CD AS 'Validate in Cambodia'
DEFAULT FALSE
ENDDEFINE
DEFINE BOOLEAN VAL_MA AS 'Validate in Morocco'
DEFAULT FALSE
ENDDEFINE
DEFINE BOOLEAN VAL_MD AS 'Validate in Republic of Moldova'
DEFAULT FALSE
ENDDEFINE
DEFINE BOOLEAN VAL_TN AS 'Validate in Tunisia'
DEFAULT FALSE
ENDDEFINE

COMPUTE FEE OFF_BasicNationalFee
YIELD 130
ENDCOMPUTE

COMPUTE FEE OFF_DesignationFee
YIELD 630
ENDCOMPUTE

COMPUTE FEE OFF_ExtensionFee OPTIONAL
YIELD 102 IF Extension EQ TRUE
ENDCOMPUTE

COMPUTE FEE OFF_SheetFee
YIELD 16*(SheetCount-35) IF SheetCount GT 35
ENDCOMPUTE

COMPUTE FEE OFF_ClaimFee
LET CF1 AS 250
LET CF2 AS 630
YIELD CF1*(ClaimCount-15) IF ClaimCount GT 15 AND ClaimCount LT 51
YIELD CF2*(ClaimCount-50) + CF1*(ClaimCount-15-(ClaimCount-50)) IF ClaimCount GT 50
ENDCOMPUTE

COMPUTE FEE OFF_SearchFee
LET SF1 AS 1390
LET SF2 AS 1185

YIELD 0 IF ISA EQ ISA_EPO
YIELD SF1-SF2 IF ISA EQ ISA_AT OR ISA EQ ISA_FI OR ISA EQ ISA_NPI OR ISA EQ ISA_ES OR ISA EQ ISA_SE OR ISA EQ ISA_TR OR ISA EQ ISA_VI
YIELD SF1 IF ISA EQ ISA_NONE
ENDCOMPUTE

COMPUTE FEE OFF_ExaminationFee
LET ExFee1 AS 1955
LET ExFee2 AS 1750
LET Discount AS 0.75

CASE IPRP EQ IPRP_EPO AS # discount scheme if EPO has prepared IPRP (Chapter II)
YIELD ExFee1*Discount IF ISA EQ ISA_EPO 
YIELD ExFee2*Discount IF ISA NEQ ISA_EPO
ENDCASE

CASE IPRP NEQ IPRP_EPO AS # no discount scheme
YIELD ExFee1 IF ISA EQ ISA_EPO
YIELD ExFee2 IF ISA NEQ ISA_EPO
ENDCASE

ENDCOMPUTE

COMPUTE FEE OFF_ValidationFee_CD
YIELD 180 IF VAL_CD EQ TRUE
ENDCOMPUTE
COMPUTE FEE OFF_ValidationFee_MA
YIELD 240 IF VAL_MA EQ TRUE
ENDCOMPUTE
COMPUTE FEE OFF_ValidationFee_MD
YIELD 200 IF VAL_MD EQ TRUE
ENDCOMPUTE
COMPUTE FEE OFF_ValidationFee_TN
YIELD 180 IF VAL_TN EQ TRUE
ENDCOMPUTE

# need to check if due (3 years after filing date)
COMPUTE FEE OFF_Renewal3
YIELD 505
ENDCOMPUTE