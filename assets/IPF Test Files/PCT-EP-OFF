# File name: PCT-EP-OFF
# File content: Official fees for PCT regional phase: EPO
# Valid until 31/03/2023

DEFINE NUMBER SheetCount AS 'Number of sheets?'
BETWEEN 1 AND 100000
DEFAULT 15
ENDDEFINE

DEFINE NUMBER ClaimCount AS 'Number of claims?'
BETWEEN 1 AND 100
DEFAULT 15
ENDDEFINE

# International Search Authority
DEFINE LIST ISA AS 'Which ISA searched the PCT application?'
CHOICE 'NONE OF THE LIST' AS ISA_NONE
CHOICE 'EPO' AS ISA_EPO
CHOICE 'AT PO' AS ISA_AT
CHOICE 'FI PRH' AS ISA_FI
CHOICE 'NPI' AS ISA_NPI
CHOICE 'ES OEPM' AS ISA_ES
CHOICE 'SE PRV' AS ISA_SE
CHOICE 'TR Turkpatent' AS ISA_TR
CHOICE 'Visegrad Patent Institute' AS ISA_VI
DEFAULT ISA_NONE
ENDDEFINE

# International preliminary report on patentability (Chapter II of the PCT)
DEFINE LIST IPRP AS 'Which office provided an IPRP (Chapter II)?'
CHOICE 'NONE' AS IPRP_NONE
CHOICE 'EPO' AS IPRP_EPO
CHOICE 'USPTO' AS IPRP_USPTO
# supplementing other IPEAs needed
DEFAULT IPRP_NONE
ENDDEFINE

DEFINE BOOLEAN Extension AS 'Designate Bosnia and Herzegovina?'
DEFAULT FALSE
ENDDEFINE

DEFINE BOOLEAN VAL_CD AS 'Validate in Cambodia'
DEFAULT FALSE
ENDDEFINE
DEFINE BOOLEAN VAL_MA AS 'Validate in Morocco'
DEFAULT FALSE
ENDDEFINE
DEFINE BOOLEAN VAL_MD AS 'Validate in Republic of Moldova'
DEFAULT FALSE
ENDDEFINE
DEFINE BOOLEAN VAL_TN AS 'Validate in Tunisia'
DEFAULT FALSE
ENDDEFINE

COMPUTE FEE OFF_BasicNationalFee
YIELD 130
ENDCOMPUTE

COMPUTE FEE OFF_DesignationFee
YIELD 630
ENDCOMPUTE

COMPUTE FEE OFF_ExtensionFee OPTIONAL
YIELD 102 IF Extension EQUALS TRUE
ENDCOMPUTE

COMPUTE FEE OFF_SheetFee
YIELD 16*(SheetCount-35) IF SheetCount ABOVE 35
ENDCOMPUTE

COMPUTE FEE OFF_ClaimFee
YIELD 250*(ClaimCount-15) IF ClaimCount ABOVE 15 AND ClaimCount BELOW 51
YIELD 630*(ClaimCount-50)+250*(ClaimCount-15-(ClaimCount-50)) IF ClaimCount ABOVE 50
ENDCOMPUTE

COMPUTE FEE OFF_SearchFee
YIELD 0 IF ISA EQUALS ISA_EPO
YIELD 1390-1185 IF ISA EQUALS ISA_AT OR ISA EQUALS ISA_FI OR ISA EQUALS ISA_NPI OR ISA EQUALS ISA_ES OR ISA EQUALS ISA_SE OR ISA EQUALS ISA_TR OR ISA EQUALS ISA_VI
YIELD 1390 IF ISA EQUALS ISA_NONE
ENDCOMPUTE

COMPUTE FEE OFF_ExaminationFee
YIELD 1955*0.75 IF ISA EQUALS ISA_EPO AND IPRP EQUALS IPRP_EPO # discount scheme
YIELD 1750*0.75 IF ISA NOTEQUALS ISA_EPO AND IPRP EQUALS IPRP_EPO # discount scheme
YIELD 1955 IF ISA EQUALS ISA_EPO
YIELD 1750 IF ISA NOTEQUALS ISA_EPO
ENDCOMPUTE

COMPUTE FEE OFF_ValidationFee_CD
YIELD 180 IF VAL_CD EQUALS TRUE
ENDCOMPUTE
COMPUTE FEE OFF_ValidationFee_MA
YIELD 240 IF VAL_MA EQUALS TRUE
ENDCOMPUTE
COMPUTE FEE OFF_ValidationFee_MD
YIELD 200 IF VAL_MD EQUALS TRUE
ENDCOMPUTE
COMPUTE FEE OFF_ValidationFee_TN
YIELD 180 IF VAL_TN EQUALS TRUE
ENDCOMPUTE

# need to check if due (3 years after filing date)
COMPUTE FEE OFF_Renewal3
YIELD 505
ENDCOMPUTE